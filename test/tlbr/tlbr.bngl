# Trivalent ligand - bivalent receptor

begin parameters
	

	## Performance test
	#nR 10000000
	
	#Lig_tot nR-(0.0667*nR)
	#Rec_tot 0.0667*nR
	#cTot 0.84	
	#koff 0.01
	#beta 0.1  # vary beta from 0.01 to 1000

	
	## Ramping Trajectory
	Lig_tot  2000
	Rec_tot  3000
	cTot     0.11
	beta     16.8
	koff     0.01
	
	
	## Peaking Trajectory
	#Lig_tot  50000
	#Rec_tot  3000
	#cTot  2.7
	#beta  16.8
	#koff  0.01
	
	
	kp1 (cTot*koff)/(3.0*Lig_tot) #FREE BINDING RATE
	kp2 (beta*koff)/Rec_tot #CROSSLINKING RATE
end parameters

begin molecule types
	L(r,r,r)
	R(l,l)
end molecule types

begin species
	L(r,r,r)	Lig_tot
	R(l,l)		Rec_tot
end species

begin reaction rules
	R(l!1).L(r!1) -> R(l) + L(r) koff
	L(r,r,r) + R(l) -> L(r!1,r,r).R(l!1) kp1
	L(r,r,r!+) + R(l) -> L(r!1,r,r!+).R(l!1) kp2
	L(r,r!+,r!+) + R(l) -> L(r!1,r!+,r!+).R(l!1) kp2
end reaction rules

begin observables
	Molecules Rfree R(l,l)
	Molecules Lfree L(r,r,r)
end observables


# Run this to test bng performance
#generate_network({max_iter=>1,overwrite=>1});
#simulate_ssa({suffix=>ssa,t_end=>3000,n_steps=>30});

writeXML();

